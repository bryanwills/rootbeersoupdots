#%Module

module-whatis Homebrew environment setup
module-help This encapsulates `brew shellenv` as a module and sets other environment configurations

switch -- [uname sysname] {
    Darwin {
        switch -- [uname machine] {
            arm64  { set brew_prefix [file join /opt homebrew] }
            x86_64 { set brew_prefix [file join /usr local]    }
        }
        set shellenv_source_prefix $brew_prefix
    }
    Linux {
        set brew_prefix [file join [file home linuxbrew] .linuxbrew]
        set shellenv_source_prefix [file join $brew_prefix Homebrew]
    }
}

set brew [file join $brew_prefix bin brew]

set shellenv_source_script [file join $shellenv_source_prefix Library Homebrew cmd shellenv.sh]
set cache_dir [file join [getenv USER_MODULESHOME] cache]
file mkdir $cache_dir

set cached_shellenv [file join $cache_dir brew-shellenv.bash]
if {![file exists $cached_shellenv] || [file mtime $cached_shellenv] < [file mtime $shellenv_source_script]} {
    set f [open $cached_shellenv w]
    puts $f [exec $brew shellenv]
    close $f
}

source-sh bash $cached_shellenv
source-sh bash [file join $brew_prefix completions bash brew]

setenv HOMEBREW_CURLRC            1
setenv HOMEBREW_FORCE_BREWED_CURL 1
setenv HOMEBREW_NO_AUTO_UPDATE    1

